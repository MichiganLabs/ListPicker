apply plugin: 'com.android.application'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.1'
    }
}

repositories {
    jcenter()
}


// If running on CI environment, apply signing configs from environment
def envSigningFile = file("$rootProject.projectDir/ListPickerDemo/signing/env.gradle")
// Otherwise load from local file if possible
def userSigningFile = file("$rootProject.projectDir/ListPickerDemo/signing/user.gradle")
if (System.getenv('ANDROID_STORE_PASS') != null) {
    logger.info('Signing config: using environment variables')
    apply from: envSigningFile
} else if (userSigningFile.exists()) {
    logger.info('Signing config: using user.gradle file')
    apply from: userSigningFile
} else {
    logger.warn('Signing config: No valid signing configuration was found!')
    logger.warn('Signing config: You will not be able to generate a signed release build!')
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "com.michiganlabs.listpickerdemo"
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile project(':ListPicker')
}
